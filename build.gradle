plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
}

group 'zone.rong'
version '1.3'

tasks.withType(JavaExec).configureEach {
    jvmArgs '--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED'
    jvmArgs '--add-opens=java.base/jdk.internal.reflect=ALL-UNNAMED'
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED", '--add-exports=java.base/jdk.internal.reflect=ALL-UNNAMED']
}

idea.project.settings.compiler.javac.javacAdditionalOptions = compileJava.options.compilerArgs.join(' ')

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    enabled = false
    finalizedBy 'testVendorsAndVersions'
}

classes {
    dependsOn ':natives:assemble'
}

def testVendorsAndVersions = tasks.register('testVendorsAndVersions') {
    group 'verification'
}

def targetVendors = [
        (JvmVendorSpec.AZUL): ["Azul", 17, 21],
        (JvmVendorSpec.BELLSOFT): ["Bellsoft", 17, 21],
        (JvmVendorSpec.GRAAL_VM): ["Graal", 17, 21],
        (JvmVendorSpec.IBM): ["OpenJ9", 17, 21],
        (JvmVendorSpec.MICROSOFT): ["Microsoft", 17, 21],
]

targetVendors.each { spec, info ->
    for (int i = 1; i < info.size(); i++) {
        def javaVersion = info[i]
        def testTask = tasks.register("test${info[0]}_${javaVersion}", Test) { Test test ->
            group 'verification'
            test.javaLauncher = javaToolchains.launcherFor { JavaToolchainSpec toolchain ->
                toolchain.languageVersion = JavaLanguageVersion.of(javaVersion)
                toolchain.vendor = spec
            }
            test.useJUnitPlatform()
            test.systemProperty "java.library.path", file("${project(":natives").buildDir}/lib/main/debug/").absolutePath + ';' + file("output").absolutePath
        }
        testVendorsAndVersions.configure {
            dependsOn testTask
        }
    }
}
